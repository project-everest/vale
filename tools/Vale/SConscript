#
# Main file for building the Vale tool
#

# Python imports
import os, os.path
import sys
import collections
# Scons imports
import SCons.Util

# Imported identifiers defined in the SConstruct file
Import('env')

####################################################################
#
#   Code to drive F#, FSLEX, and FSYACC tools
#
####################################################################

BUILD = '#tools/FsLexYacc/FsLexYacc.6.1.0/build'    # the '#' character makes this relative to the SConstruct file in the root of the repo
FSLEX = BUILD+'/fslex.exe'
FSYACC = BUILD+'/fsyacc.exe'
BIN = '#bin'

def fsyaccEmitter(target, source, env):
  # assume the initial target is a .fs file.  Add the .fsi target
  targetBase, targetExt = os.path.splitext(SCons.Util.to_String(target[0]))
  target.append(targetBase+'.fsi')
  return (target, source)

def add_fslexyacc(env):
  # probe for fslexyacc to ensure it is installed ahead of trying to use it to build
  fslexfile = File(FSLEX)
  if not os.path.isfile(str(fslexfile)):
    raise EnvironmentError('FsLexYacc missing.  Please install to: "%s"'%str(fslexfile))
    
  env['FSLEX'] = fslexfile
  fslex = Builder(action='$MONO $FSLEX $SOURCE -o $TARGET',
                           suffix = '.fs',
                           src_suffix = '.fsl')
  env.Append(BUILDERS = {'FSLex' : fslex})
 
  env['FSYACC'] = File(FSYACC)
  fsyacc = Builder(action='$MONO $FSYACC --module "Parse" $SOURCE -o $TARGET',
                           suffix = '.fs',
                           src_suffix = '.fsy',
                           emitter = fsyaccEmitter)
  env.Append(BUILDERS = {'FSYacc' : fsyacc})

def build_vale(sources, dafny_refs):
  senv = env.Clone()
  # Wrap all paths in File() so they are appropriately adjusted to be relative to the SConstruct file
  senv['DAFNY_REFS']=[]
  for r in dafny_refs:
    senv.Append(DAFNY_REFS = '-r')
    senv.Append(DAFNY_REFS = File(r))
  if sys.platform != 'win32':
    senv['FSC']='fsharpc'
  else:
    # probe for the F# compiler on the PATH
    fsc = FindFile('fsc.exe', os.environ['PATH'].split(';'))
    if fsc == None:
      installdir = os.environ.get('FSHARPINSTALLDIR')
      if installdir == None:
        raise EnvironmentError('fsc,exe missing from path and FSHARPINSTALLDIR.')
      senv['FSC']=File(os.path.join(installdir, 'fsc.exe'))
    else:
      senv['FSC']=fsc
  vale_tool = senv.Command(BIN+'/vale.exe', sources,
    '$FSC -g --platform:anycpu --standalone --mlcompatibility -O $SOURCES -o $TARGET $DAFNY_REFS')

  # add dependencies to vale.exe
  for r in dafny_refs:
    Depends(vale_tool, r)
  return vale_tool

add_fslexyacc(env)

####################################################################
#
#   Describe the files needed to build Vale
#
####################################################################

DAFNY_BIN = [
    'BoogieAbsInt.dll',
    'BoogieBasetypes.dll',
    'BoogieCodeContractsExtender.dll',
    'BoogieConcurrency.dll',
    'BoogieCore.dll',
    'BoogieDoomed.dll',
    'BoogieExecutionEngine.dll',
    'BoogieGraph.dll',
    'BoogieHoudini.dll',
    'BoogieModel.dll',
    'BoogieParserHelper.dll',
    'BoogieVCExpr.dll',
    'BoogieVCGeneration.dll',
    'DafnyPipeline.dll',
    'Dafny.exe'
  ]

SRC = [
  'src/ast.fs',
  'src/ast_util.fs',
  'src/parse_util.fs',
  '../../obj/Vale/parse.fs',
  '../../obj/Vale/lex.fs',
  'src/transform.fs',
  'src/emit_common.fs',
  'src/emit_dafny_text.fs',
  'src/emit_dafny_direct.fs',
  'src/emit_fstar_text.fs',
  'src/emit_vale_text.fs',
  'src/main.fs'
  ]

# Dafny and Boogie binaries.  Z3 is handled separately.
VALE_BIN = DAFNY_BIN + [
  'DafnyPrelude.bpl',
  'DafnyRuntime.cs',
  'Provers.SMTLib.dll',
  ]

# build DafnyInterface.dll
DafnyInterface_dir = Dir('DafnyInterface/Source/DafnyInterface')
DafnyInterface_src = [DafnyInterface_dir.File(f)
                      for f in ['DafnyDriver.cs', 'Properties/AssemblyInfo.cs']]
DafnyInterface_refs = [Dir(env['DAFNY_PATH']).File(f).abspath for f in [
    'BoogieAbsInt.dll',
    'BoogieConcurrency.dll',
    'BoogieCore.dll',
    'BoogieExecutionEngine.dll',
    'BoogieParserHelper.dll',
    'BoogieVCGeneration.dll',
    'Dafny.exe',
    'DafnyPipeline.dll',
    'Provers.SMTLib.dll',
]]

if sys.platform == 'win32':
  # The default search path for env.Detect() is just windows\system32.
  # Search along the OS PATH, then quote the name to avoid spaces-in-path issues.
  env['CSC'] = '\"' + env.WhereIs('csc.exe', path=os.environ['PATH']) + '\"'
else: # mono
  env['CSC'] = env.Detect('dmcs')

DafnyInterface_dll = env.Command(
  '#bin/DafnyInterface.dll',
  DafnyInterface_src,
  '$CSC -t:library -out:$TARGET $SOURCES '
    + ' '.join(['-r:'+r for r in DafnyInterface_refs]))
Depends(DafnyInterface_dll, DafnyInterface_refs)

DAFNY_REFS = [Dir(env['DAFNY_PATH']).File(f).abspath for f in DAFNY_BIN] + [
  DafnyInterface_dll,
  '../Dafny/Newtonsoft.Json.dll',
  '../../tools/FsLexYacc/FsLexYacc.Runtime.6.1.0/lib/net40/FsLexYacc.Runtime.dll'
  ]

# build vale.exe
env.FSLex(target='../../obj/Vale/lex.fs', source='src/lex.fsl')
env.FSYacc(target='../../obj/Vale/parse.fs', source='src/parse.fsy')
vale_tool = build_vale(SRC, DAFNY_REFS)

# copy the dependencies to the BIN directory, too
dependencies = vale_tool
for s in VALE_BIN:
  b = env.CopyAs(Dir(BIN).File(s), Dir(env['DAFNY_PATH']).File(s))
  Depends(vale_tool, b)
  dependencies.append(b)
if sys.platform == 'win32' or (
    sys.platform.startswith('linux')
        and os.path.exists('/proc/sys/fs/binfmt_misc/WSLInterop')):
  # we can use our win32 z3 binary, either natively or via WSL interop
  b = env.CopyAs(Dir(BIN).File('z3.exe'), Dir(env['DAFNY_PATH']).File('z3.exe'))
  Depends(vale_tool, b)
  dependencies.append(b)
  z3 = File(b[0])
else:
  z3 = FindFile('z3', os.environ['PATH'].split(':'))
  if z3 == None:
    raise EnvironmentError('z3 missing from path.')
  Depends(vale_tool, z3)
  dependencies.append(z3)
  
# Return a list containing the vale.exe tool and its dependent DLLs, for
# other scripts to use as additional dependencies.
# Also return the path to z3 relative to the root Sconstruct file.
vale_tool_results = collections.namedtuple('ValeToolResults', 'dependencies z3')(dependencies, z3)
Return('vale_tool_results')
