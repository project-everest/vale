include "../../../arch/x64/X64.Vale.InsBasic.vaf"
include "../../../arch/x64/X64.Vale.InsMem.vaf"
include "../../../arch/x64/X64.Vale.InsVector.vaf"
include "X64.AES.vaf"
include "X64.GCTR.vaf"
include "X64.GHash.vaf"

module X64.GCM

#verbatim{:interface}{:implementation}
open FStar.Seq
open Words_s
open Types_s
open Types_i
open AES_s
open GCTR_s
open GHash_s
open GHash_i
open GCM_s
open X64.AES
open X64.GHash
open X64.GCTR
open X64.Machine_s
open X64.Memory_i_s
open X64.Vale.State_i
open X64.Vale.Decls_i
open X64.Vale.InsBasic
open X64.Vale.InsMem
open X64.Vale.InsVector
open X64.Vale.InsAes
open X64.Vale.QuickCode_i
open X64.Vale.QuickCodes_i
#endverbatim

#verbatim{:interface}
//let get_last_slice_workaround (s:seq quad32) (start_pos end_pos:int)  =
//  if 0 <= start_pos && start_pos < end_pos && end_pos <= length s then
//    last (slice s start_pos end_pos)
//  else
//    Mkfour 0 0 0 0 
//
//let slice_workaround (s:seq quad32) (start_pos end_pos:int)  =
//  if 0 <= start_pos && start_pos < end_pos && end_pos <= length s then
//    slice s start_pos end_pos
//  else
//    create 1 (Mkfour 0 0 0 0)
#endverbatim

#reset-options "--z3rlimit 100"

///////////////////////////
// GCM
///////////////////////////

procedure {:quick} gcm_core(
    ghost iv_BE:quad32,
    ghost plain_b:buffer128,
    ghost auth_b:buffer128,
    ghost out_b:buffer128,
    ghost key:aes_key_LE(AES_128),
    ghost round_keys:seq(quad32),
    ghost keys_b:buffer128
    )

    lets plain_ptr @= rax; out_ptr @= rbx; keys_ptr @= r8; auth_ptr @= r14;
    plain_len @= r13; auth_len @= r11; 
    iv @= xmm7; mask @= xmm8;

    reads
        out_ptr; keys_ptr; auth_ptr; plain_len; auth_len; 

    modifies
        rax; rcx; rdx; r9; r10; r12; xmm0; xmm1; xmm2; xmm3; xmm4; xmm5; xmm11; iv; mask;
        xmm6; 
        mem; efl; 


    requires
        // GCM reqs
        iv == reverse_bytes_quad32(iv_BE);
        buffers_disjoint128(plain_b, out_b);
        buffers_disjoint128(auth_b, out_b);
        buffers_disjoint128(keys_b, out_b);
        validSrcAddrs128(mem, plain_ptr, plain_b, plain_len);
        validSrcAddrs128(mem, auth_ptr, auth_b, auth_len);
        validDstAddrs128(mem, out_ptr, out_b, plain_len);
        plain_ptr + 16 * plain_len < pow2_64;
        auth_ptr + 16 * auth_len < pow2_64;
        out_ptr + 16 * plain_len < pow2_64;
        buffer_length(plain_b) == buffer_length(out_b) /\ buffer_length(out_b) == plain_len /\ 256 * buffer_length(plain_b) < pow2_32;
        buffer_length(auth_b) == auth_len;

        // To simplify length calculations, restrict auth and plain length further
        256 * auth_len < pow2_32;
        256 * plain_len < pow2_32;

        // AES reqs
        length(round_keys) == 11;
        round_keys == key_to_round_keys_LE(AES_128, key);
        validSrcAddrs128(mem, keys_ptr, keys_b, 11);
        buffer128_as_seq(mem, keys_b) == round_keys;
    ensures
        modifies_buffer128(out_b, old(mem), mem);
        validSrcAddrs128(mem, out_ptr, out_b, plain_len);
        256 * buffer_length(plain_b) < pow2_32;
        256 * buffer_length(auth_b) < pow2_32;
        buffer128_as_seq(mem, out_b) == fst(gcm_encrypt_LE(AES_128, key, iv_BE, buffer128_as_seq(old(mem), plain_b), buffer128_as_seq(old(mem), auth_b)));
        create(1, xmm1) == snd(gcm_encrypt_LE(AES_128, key, iv_BE, buffer128_as_seq(old(mem), plain_b), buffer128_as_seq(old(mem), auth_b)));
{
    // let h = aes_encrypt_LE alg key (Mkfour 0 0 0 0) in
    ZeroXmm(xmm0);
    AES128EncryptBlock(xmm0, key, round_keys, keys_b); // h = xmm0 = aes_encrypt_LE alg key (Mkfour 0 0 0 0) in
    ghost var h := xmm0;
    Mov128(xmm11, xmm0);     // Save a copy of h

    // let j0_BE = Mkfour 1 iv_BE.lo1 iv_BE.hi2 iv_BE.hi3 in
    // (inc32 j0 1)
    InitPshufbMask(xmm8, r12);
    Pshufb(iv, xmm8); // to big endian
    PinsrdImm(iv, 2, 0, r12);
    // assert iv == inc32(Mkfour(1, iv_BE.lo1, iv_BE.hi2, iv_BE.hi3), 1);

    // let c = gctr_encrypt_LE (inc32 j0 1) p alg key in
    //Mov64(rax, plain_ptr);
    Mov64(rcx, plain_len);
    gctr_core(iv, plain_b, out_b, key, round_keys, keys_b);

    // Compute the hashes incrementally
    Mov64(rax, auth_ptr);
    Mov64(rcx, auth_len);
    ghost var y_0 := Mkfour(0, 0, 0, 0);
    compute_Y0();
    // assert xmm1 == y_0;
    compute_ghash_incremental(auth_b);
    ghost var y_auth := xmm1;

    Mov64(rax, out_ptr);
    Mov64(rcx, plain_len);

    compute_ghash_incremental(out_b);
    ghost var y_cipher := xmm1;

    // Prepare length fields
    ZeroXmm(xmm2);
    Mov64(rax, plain_len);
    IMul64(rax, 128);
    Pinsrd(xmm2, rax, 0);
    Mov64(rax, auth_len);
    IMul64(rax, 128);
    Pinsrd(xmm2, rax, 2);
    //InitPshufbMask(xmm0, r12);
    Pshufb(xmm2, xmm8);

    ghost var length_quad32 := xmm2;

    compute_ghash_incremental_register();
    ghost var y_final := xmm1;

    // Invoke lemma showing that incremental hashing works
    lemma_hash_append3(h, y_0, y_auth, y_cipher, y_final,
                       buffer128_as_seq(mem, auth_b),
                       buffer128_as_seq(mem, out_b),
                       create(1, length_quad32)); 
    // assert xmm1 == ghash_LE(h, append(buffer128_as_seq(mem, auth_b), append(buffer128_as_seq(mem, out_b), create(1, length_quad32)))); 
    ghost var hash := xmm1;

    PinsrdImm(iv, 1, 0, r12);   // Reconstruct j0 (this is all we need, since gctr_core says it only changes iv.lo0)
    // assert iv == iv_BE.(lo0 := 1);

    // Encrypt the hash value with gctr_register 
    gctr_register(key, round_keys, keys_b); // Encrypt using j0 and xmm1 = hash_value 
    // assert create(1, xmm1) == gctr_encrypt_LE(iv_BE.(lo0 := 1), create(1, hash), AES_128, key);
}


// notypecheck: cannot unify type "X64.Vale.Decls_i.va_operand_reg_opr64" and "X64.Vale.Decls_i.va_reg_operand"
// Store64_buffer(ptr, ...)
// the inferred type for "ptr" is "X64.Vale.Decls_i.va_reg_operand", 
// the paramter type for "ptr" is "X64.Vale.Decls_i.va_operand_reg_opr64"
procedure {:quick}{:notypecheck} callee_save_registers(ghost buffer:buffer64)
    lets ptr @= rax;
    requires 
        validDstAddrs64(mem, ptr, buffer, 8);
    reads rax; rbx; rbp; rdi; rsi; r12; r13; r14; r15; 
    modifies mem;
    ensures
        validDstAddrs64(mem, ptr, buffer, 8);
        modifies_buffer(buffer, old(mem), mem);

        rbx == buffer64_read(buffer, 0, mem);
        rbp == buffer64_read(buffer, 1, mem);
        rdi == buffer64_read(buffer, 2, mem);
        rsi == buffer64_read(buffer, 3, mem);
        r12 == buffer64_read(buffer, 4, mem);
        r13 == buffer64_read(buffer, 5, mem);
        r14 == buffer64_read(buffer, 6, mem);
        r15 == buffer64_read(buffer, 7, mem);
{
    Store64_buffer(ptr, rbx,  0, buffer, 0);
    Store64_buffer(ptr, rbp,  8, buffer, 1);
    Store64_buffer(ptr, rdi, 16, buffer, 2);
    Store64_buffer(ptr, rsi, 24, buffer, 3);
    Store64_buffer(ptr, r12, 32, buffer, 4);
    Store64_buffer(ptr, r13, 40, buffer, 5);
    Store64_buffer(ptr, r14, 48, buffer, 6);
    Store64_buffer(ptr, r15, 56, buffer, 7);
}

// notypecheck: cannot unify type "X64.Vale.Decls_i.va_operand_reg_opr64" and "X64.Vale.Decls_i.va_reg_operand"
// Store64_buffer(ptr, ...)
// the inferred type for "ptr" is "X64.Vale.Decls_i.va_reg_operand", 
// the paramter type for "ptr" is "X64.Vale.Decls_i.va_operand_reg_opr64"
procedure {:quick}{:notypecheck} callee_restore_registers(ghost buffer:buffer64)
    lets ptr @= rax;
    requires 
        validSrcAddrs64(mem, ptr, buffer, 8);
    reads rax; mem; 
    modifies rbx; rbp; rdi; rsi; r12; r13; r14; r15; 
    ensures
        rbx == buffer64_read(buffer, 0, mem);
        rbp == buffer64_read(buffer, 1, mem);
        rdi == buffer64_read(buffer, 2, mem);
        rsi == buffer64_read(buffer, 3, mem);
        r12 == buffer64_read(buffer, 4, mem);
        r13 == buffer64_read(buffer, 5, mem);
        r14 == buffer64_read(buffer, 6, mem);
        r15 == buffer64_read(buffer, 7, mem);
{
    Load64_buffer(rbx, ptr,  0, buffer, 0);
    Load64_buffer(rbp, ptr,  8, buffer, 1);
    Load64_buffer(rdi, ptr, 16, buffer, 2);
    Load64_buffer(rsi, ptr, 24, buffer, 3);
    Load64_buffer(r12, ptr, 32, buffer, 4);
    Load64_buffer(r13, ptr, 40, buffer, 5);
    Load64_buffer(r14, ptr, 48, buffer, 6);
    Load64_buffer(r15, ptr, 56, buffer, 7);
}

#verbatim{:interface}
let mk_gctr_plain (p:seq quad32) : gctr_plain_LE =
  let open FStar.Mul in
  if 256 * length p < pow2_32 then p else createEmpty
#endverbatim

function mk_gctr_plain (p:(seq (quad32))) : gctr_plain_LE extern;

procedure {:quick} gcm_stdcall_inner(
    ghost args_b:buffer64,
    ghost iv_BE:quad32,

    ghost plain_b:buffer128,
    ghost auth_b:buffer128,
    ghost iv_b:buffer128,
    ghost out_b:buffer128,
    ghost tag_b:buffer128,

    ghost key:aes_key_LE(AES_128),
    ghost round_keys:seq(quad32),
    ghost keys_b:buffer128
    )
    modifies
        rax; rbx; rcx; rdx; r8; r9; r10; r11; r12; r13; r14; r15; xmm0; xmm1; xmm2; xmm3; xmm4; xmm5; xmm6; xmm7; xmm8; xmm11;
        mem; efl;
    lets
        args_ptr := r9;

        plain_ptr         := buffer64_read(args_b, 0, mem);
        plain_len         := buffer64_read(args_b, 1, mem);
        auth_ptr          := buffer64_read(args_b, 2, mem);
        auth_len          := buffer64_read(args_b, 3, mem);
        iv_ptr            := buffer64_read(args_b, 4, mem);
        expanded_key_ptr  := buffer64_read(args_b, 5, mem);
        out_ptr           := buffer64_read(args_b, 6, mem);
        tag_ptr           := buffer64_read(args_b, 7, mem);

    requires
        validSrcAddrs64(mem, args_ptr, args_b, 9);

        buffers_disjoint128(plain_b, out_b);
        buffers_disjoint128(auth_b, out_b);
        buffers_disjoint128(keys_b, out_b);
        buffers_disjoint128(out_b, tag_b);
        
        validSrcAddrs128(mem, plain_ptr, plain_b, plain_len);
        validSrcAddrs128(mem, auth_ptr, auth_b, auth_len);
        validSrcAddrs128(mem, iv_ptr, iv_b, 1);
        validDstAddrs128(mem, out_ptr, out_b, plain_len);
        validDstAddrs128(mem, tag_ptr, tag_b, 1);

        plain_ptr + 16 * plain_len < pow2_64;
        auth_ptr + 16 * auth_len < pow2_64;
        out_ptr + 16 * plain_len < pow2_64;
        buffer_length(plain_b) == buffer_length(out_b) /\ buffer_length(out_b) == plain_len /\ 256 * buffer_length(plain_b) < pow2_32;
        buffer_length(auth_b) == auth_len;

        iv_BE == reverse_bytes_quad32(buffer128_read(iv_b, 0, mem));

        // To simplify length calculations, restrict auth and plain length further
        256 * auth_len < pow2_32;
        256 * plain_len < pow2_32;

        // AES reqs
        length(round_keys) == 11;
        round_keys == key_to_round_keys_LE(AES_128, key);
        validSrcAddrs128(mem, expanded_key_ptr, keys_b, 11);
        buffer128_as_seq(mem, keys_b) == round_keys;

    ensures
        modifies_buffer128_2(out_b, tag_b, old(mem), mem);

        validSrcAddrs128(mem, old(out_ptr), out_b, plain_len);
        validSrcAddrs128(mem, old(tag_ptr), tag_b, 1);

        256 * buffer_length(plain_b) < pow2_32;
        256 * buffer_length(auth_b) < pow2_32;
        buffer128_as_seq(mem, out_b) == fst(gcm_encrypt_LE(AES_128, key, iv_BE, buffer128_as_seq(old(mem), plain_b), buffer128_as_seq(old(mem), auth_b)));
        create(1, buffer128_read(tag_b, 0, mem)) == snd(gcm_encrypt_LE(AES_128, key, iv_BE, mk_gctr_plain(buffer128_as_seq(old(mem), plain_b)), mk_gctr_plain(buffer128_as_seq(old(mem), auth_b))));
{
    Load64_buffer(rax, r9,  0, args_b, 0);
    //assert rax == plain_ptr;
    Load64_buffer(r13, r9,  8, args_b, 1);
    //assert r13 == plain_len;
    Load64_buffer(r14, r9, 16, args_b, 2);
    Load64_buffer(r11, r9, 24, args_b, 3);
    Load64_buffer(r10, r9, 32, args_b, 4);
    Load64_buffer(r8,  r9, 40, args_b, 5);
    Load64_buffer(rbx, r9, 48, args_b, 6);
    Load64_buffer(r15, r9, 56, args_b, 7);

    // Load the IV into its XMM register
    Load128_buffer(xmm7, r10, 0, iv_b, 0);

    //assert iv_BE == reverse_bytes_quad32(xmm7);
//    assert {:quick_end} true;

    gcm_core(iv_BE, plain_b, auth_b, out_b, key, round_keys, keys_b);

//    assert buffer128_as_seq(mem, out_b) == fst(gcm_encrypt_LE(AES_128, key, iv_BE, mk_gctr_plain(buffer128_as_seq(old(mem), plain_b)), mk_gctr_plain(buffer128_as_seq(old(mem), auth_b))));

    //assert create(1, xmm1) == snd(gcm_encrypt_LE(AES_128, key, iv_BE, mk_gctr_plain(buffer128_as_seq(old(mem), plain_b)), mk_gctr_plain(buffer128_as_seq(old(mem), auth_b))));
    // Auth tag is still in xmm1, so save it to memory
    Store128_buffer(r15, xmm1, 0, tag_b, 0);
    //assert buffer128_as_seq(mem, tag_b) == create(1, xmm1);
    //assert buffer128_read(tag_b, 0, mem) == create(1, xmm1);

    //assert create(1, buffer128_read(tag_b, 0, mem)) == snd(gcm_encrypt_LE(AES_128, key, iv_BE, mk_gctr_plain(buffer128_as_seq(old(mem), plain_b)), mk_gctr_plain(buffer128_as_seq(old(mem), auth_b))));
    //assert buffer128_as_seq(mem, tag_b) == snd(gcm_encrypt_LE(AES_128, key, iv_BE, mk_gctr_plain(buffer128_as_seq(old(mem), plain_b)), mk_gctr_plain(buffer128_as_seq(old(mem), auth_b))));
}

procedure {:quick} gcm_stdcall(
    inline win:bool,
    ghost args_b:buffer64,
    ghost iv_BE:quad32,

    ghost plain_b:buffer128,
    ghost auth_b:buffer128,
    ghost iv_b:buffer128,
    ghost out_b:buffer128,
    ghost tag_b:buffer128,
    ghost tmp_b:buffer64,

    ghost key:aes_key_LE(AES_128),
    ghost round_keys:seq(quad32),
    ghost keys_b:buffer128
    )
    modifies
        rax; rbx; rcx; rdx; r8; r9; r10; r11; r12; r13; r14; r15; xmm0; xmm1; xmm2; xmm3; xmm4; xmm5; xmm6; xmm7; xmm8; xmm11;
        rbp; rdi; rsi;
        mem; efl;
    lets
        args_ptr := if win then rcx else rdi;

        plain_ptr         := buffer64_read(args_b, 0, mem);
        plain_len         := buffer64_read(args_b, 1, mem);
        auth_ptr          := buffer64_read(args_b, 2, mem);
        auth_len          := buffer64_read(args_b, 3, mem);
        iv_ptr            := buffer64_read(args_b, 4, mem);
        expanded_key_ptr  := buffer64_read(args_b, 5, mem);
        out_ptr           := buffer64_read(args_b, 6, mem);
        tag_ptr           := buffer64_read(args_b, 7, mem);
        tmp_ptr           := buffer64_read(args_b, 8, mem);

    requires
        validSrcAddrs64(mem, args_ptr, args_b, 9);

        buffers_disjoint128(plain_b, out_b);
        buffers_disjoint128(auth_b, out_b);
        buffers_disjoint128(keys_b, out_b);
        buffers_disjoint128(out_b, tag_b);
        
        buffers_disjoint(args_b, tmp_b);

        locs_disjoint(list(loc_buffer(tmp_b), loc_buffer(plain_b)));
        locs_disjoint(list(loc_buffer(tmp_b), loc_buffer(auth_b)));
        locs_disjoint(list(loc_buffer(tmp_b), loc_buffer(iv_b)));
        locs_disjoint(list(loc_buffer(tmp_b), loc_buffer(out_b)));
        locs_disjoint(list(loc_buffer(tmp_b), loc_buffer(tag_b)));
        locs_disjoint(list(loc_buffer(tmp_b), loc_buffer(keys_b)));

        validSrcAddrs128(mem, plain_ptr, plain_b, plain_len);
        validSrcAddrs128(mem, auth_ptr, auth_b, auth_len);
        validSrcAddrs128(mem, iv_ptr, iv_b, 1);
        validDstAddrs128(mem, out_ptr, out_b, plain_len);
        validDstAddrs128(mem, tag_ptr, tag_b, 1);
        validDstAddrs64(mem, tmp_ptr, tmp_b, 8);

        plain_ptr + 16 * plain_len < pow2_64;
        auth_ptr + 16 * auth_len < pow2_64;
        out_ptr + 16 * plain_len < pow2_64;
        buffer_length(plain_b) == buffer_length(out_b) /\ buffer_length(out_b) == plain_len /\ 256 * buffer_length(plain_b) < pow2_32;
        buffer_length(auth_b) == auth_len;

        iv_BE == reverse_bytes_quad32(buffer128_read(iv_b, 0, mem));

        // To simplify length calculations, restrict auth and plain length further
        256 * auth_len < pow2_32;
        256 * plain_len < pow2_32;

        // AES reqs
        length(round_keys) == 11;
        round_keys == key_to_round_keys_LE(AES_128, key);
        validSrcAddrs128(mem, expanded_key_ptr, keys_b, 11);
        buffer128_as_seq(mem, keys_b) == round_keys;

    ensures
        modifies_mem(loc_union(loc_union(loc_buffer(tmp_b), loc_buffer(out_b)), loc_buffer(tag_b)), old(mem), mem);

        validSrcAddrs128(mem, old(out_ptr), out_b, plain_len);
        validSrcAddrs128(mem, old(tag_ptr), tag_b, 1);

        256 * buffer_length(plain_b) < pow2_32;
        256 * buffer_length(auth_b) < pow2_32;
        buffer128_as_seq(mem, out_b) == fst(gcm_encrypt_LE(AES_128, key, iv_BE, buffer128_as_seq(old(mem), plain_b), buffer128_as_seq(old(mem), auth_b)));
        create(1, buffer128_read(tag_b, 0, mem)) == snd(gcm_encrypt_LE(AES_128, key, iv_BE, mk_gctr_plain(buffer128_as_seq(old(mem), plain_b)), mk_gctr_plain(buffer128_as_seq(old(mem), auth_b))));

        // Calling convention for caller/callee saved registers

        // Windows:
        win ==> rbx == old(rbx);
        win ==> rbp == old(rbp);
        win ==> rdi == old(rdi);
        win ==> rsi == old(rsi);
        win ==> r12 == old(r12);
        win ==> r13 == old(r13);
        win ==> r14 == old(r14);
        win ==> r15 == old(r15);

        // Linux:
        !win ==> rbx == old(rbx);
        !win ==> rbp == old(rbp);
        !win ==> r12 == old(r12);
        !win ==> r13 == old(r13);
        !win ==> r14 == old(r14);
        !win ==> r15 == old(r15);
{
    // Shuffle the incoming pointer around
    inline if (win) {
        Mov64(r9, rcx);
    } else {
        Mov64(r9, rdi);
    }
    // assert r9 == args_ptr;

    Load64_buffer(rax, r9, 64, args_b, 8);
    callee_save_registers(tmp_b);

    // Save a copy of the tmp_ptr
    Mov64(rsi, rax);

    gcm_stdcall_inner(args_b, iv_BE, plain_b, auth_b, iv_b, out_b, tag_b, key, round_keys, keys_b);

    // Restore tmp_ptr
    Mov64(rax, rsi);
    callee_restore_registers(tmp_b);
}
